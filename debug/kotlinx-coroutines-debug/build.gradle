buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'maven-publish'
}

group = 'org.jetbrains.kotlinx'
version = '0.18-SNAPSHOT'

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'kotlin'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    }
}

dependencies {
    compile project(':kotlinx-coroutines-debug-agent')
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version" //will be add to pom.xml when publish
}

jar.enabled = false //prevent `build` task from empty jar creation

tasks.build.dependsOn shadowJar

shadowJar {
    classifier = null

    configurations = [project.configurations.compile]

    dependencies {
        include dependency('org.jetbrains.asm:asm-all.*') //from project(":kotlinx-coroutines-debug-transformer")
        include dependency(':kotlinx-coroutines-debug.*')
    }

    relocate 'org.jetbrains.org.objectweb.asm', 'kotlinx.coroutines.debug.org.objectweb.asm'
}

jar {
    manifest {
        attributes 'Premain-Class': 'kotlinx.coroutines.debug.agent.Agent'
        attributes 'Agent-Class': 'kotlinx.coroutines.debug.agent.Agent'
        attributes 'Can-Redefine-Classes': true
        attributes 'Can-Retransform-Classes': true
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}